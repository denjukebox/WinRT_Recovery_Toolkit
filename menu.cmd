@ECHO OFF
set EXEC_DRIVE_LETTER=%CD:~0,2%

:START
ECHO BEFORE CONTINUING, VERIFY THAT ALL BITLOCKERS ARE TURNED OFF
ECHO YOUR CURRENT DRIVE LETTER IS %EXEC_DRIVE_LETTER%
set /p CONFIRM_CONTINUE=CONFIRM READY FOR INSTALL [y/n]?:

IF /I "%CONFIRM_CONTINUE%"=="n" GOTO EOF
IF /I "%CONFIRM_CONTINUE%"=="N" GOTO EOF
IF /I "%CONFIRM_CONTINUE%"=="Y" GOTO MENU
IF /I "%CONFIRM_CONTINUE%"=="y" GOTO MENU
GOTO START

ECHO GOOD, LETS BEGIN

:MENU
ECHO [MAIN MENU]
ECHO:
ECHO 0 ^| EXIT
ECHO ==========================
ECHO 1 ^| AUTOMATED INSTALLS
ECHO 2 ^| PARTIONING
ECHO 3 ^| INSTALL
ECHO 4 ^| BOOT
ECHO 5 ^| UNLOCK

set /p ID=ENTER ACTION:
ECHO:
IF "%ID%"=="0" EXIT 0
IF "%ID%"=="1" CALL :AUTOMATED_INSTALLS
IF "%ID%"=="2" CALL :PARTITIONING
IF "%ID%"=="3" CALL :INSTALL
IF "%ID%"=="4" CALL :BOOT
IF "%ID%"=="5" CALL :UNLOCK
GOTO MENU

:AUTOMATED_INSTALLS
ECHO [AUTOMATED INSTALLS]
ECHO WARNING, THESE WIPE THE ENTIRE DISK
ECHO:
ECHO 0 ^| UP
ECHO ==========================
ECHO 1 ^| WINDOWS 8.0
ECHO 2 ^| WINDOWS 10

set /p ID=ENTER ACTION:
ECHO:
IF "%ID%"=="0" GOTO MENU
IF "%ID%"=="1" CALL :AUTOMATED_WIN80
IF "%ID%"=="2" CALL :AUTOMATED_WIN10
GOTO AUTOMATED_INSTALLS

:AUTOMATED_WIN80
CALL :CREATEPARTITIONS-UEFI
CALL :INSTALL_WN_RT_80
CALL :EFI
CALL :FIXBOOT
EXIT /B 0

:AUTOMATED_WIN10
CALL :PARTITION512K
CALL :INSTALL_WN_RT_10
CALL :ESPEFI
CALL :FIXBOOT
EXIT /B 0

:PARTITIONING
ECHO [PARTITIONING]
ECHO:
ECHO 0 ^| UP
ECHO ==========================
ECHO SELECT SECTOR SIZE
ECHO 1 ^| CREATEPARTITIONS-UEFI (IF INSTALL PRESENT)
ECHO 2 ^| 512K
ECHO 3 ^| 4KB

set /p ID=:
ECHO:
IF "%ID%"=="0" GOTO MENU
IF "%ID%"=="1" CALL :CREATEPARTITIONS-UEFI
IF "%ID%"=="2" CALL :PARTITION512K
IF "%ID%"=="3" CALL :PARTITION4KB
GOTO PARTITIONING

:CREATEPARTITIONS-UEFI
ECHO PARTITIONING DISK, THIS MIGHT TAKE A WHILE...
COPY /B %EXEC_DRIVE_LETTER%\SCRIPTS\DISKPART_CLEAN.TXT+%EXEC_DRIVE_LETTER%\SOURCES\CREATEPARTITIONS-UEFI.TXT %EXEC_DRIVE_LETTER%\SCRIPTS\CREATEPARTITIONS-UEFI.TXT
DISKPART /S %EXEC_DRIVE_LETTER%\SCRIPTS\CREATEPARTITIONS-UEFI.TXT
ECHO PARTITIONING DISK COMPLETE
EXIT /B 0

:PARTITION512K
ECHO PARTITIONING DISK, THIS MIGHT TAKE A WHILE...
DISKPART /S %EXEC_DRIVE_LETTER%\SCRIPTS\DISKPART_WINRT_512K.TXT
ECHO PARTITIONING DISK COMPLETE
EXIT /B 0

:PARTITION4KB
ECHO PARTITIONING DISK, THIS MIGHT TAKE A WHILE...
DISKPART /S %EXEC_DRIVE_LETTER%\SCRIPTS\DISKPART_WINRT_4KB.TXT
ECHO PARTITIONING DISK COMPLETE
EXIT /B 0

:INSTALL
ECHO [INSTALL]
ECHO:
ECHO 0 ^| UP
ECHO ==========================
ECHO 1 ^| INSTALL WINDOWS RT 8.0
ECHO 2 ^| INSTALL WINDOWS RT 10

set /p ID=ENTER ACTION:
ECHO:
IF "%ID%"=="0" GOTO MENU
IF "%ID%"=="1" CALL :INSTALL_WN_RT_80
IF "%ID%"=="2" CALL :INSTALL_WN_RT_10
GOTO INSTALL

:INSTALL_WN_RT_80
ECHO INSTALLING RT
DISM /APPLY-IMAGE /IMAGEFILE:%EXEC_DRIVE_LETTER%\SOURCES\INSTALL.WIM /INDEX:1 /APPLYDIR:W:\
ECHO WIN RT 8.0 INSTALL FINISHED
EXIT /B 0

:INSTALL_WN_RT_10
ECHO INSTALLING RT
DISM /APPLY-IMAGE /IMAGEFILE:%EXEC_DRIVE_LETTER%\SOURCES\INSTALL_10_15035.WIM /INDEX:1 /APPLYDIR:W:\
ECHO WIN RT 10 INSTALL FINISHED
CALL %EXEC_DRIVE_LETTER%\SCRIPTS\WIN10_REG.CMD %EXEC_DRIVE_LETTER%
ECHO REGISTRY PATCHES APPLIED
EXIT /B 0

:BOOT
ECHO [BOOT]
ECHO:
ECHO 0 ^| UP
ECHO ==========================
ECHO 1 ^| COPY RECOVERY EFI
ECHO 2 ^| COPY ESP EFI
ECHO 3 ^| SCAN AND ADD BOOT DEVICES
ECHO 4 ^| COPY GRUB AS BOOT (EXPERIMENTAL)

set /p ID=ENTER ACTION:
ECHO:
IF "%ID%"=="0" GOTO MENU
IF "%ID%"=="1" CALL :EFI
IF "%ID%"=="2" CALL :ESPEFI
IF "%ID%"=="3" CALL :FIXBOOT
IF "%ID%"=="4" CALL :GRUB
GOTO BOOT

:ESPEFI
ECHO COPYING ESPEFI
CALL %EXEC_DRIVE_LETTER%\SCRIPTS\ESPEFI.CMD %EXEC_DRIVE_LETTER%
EXIT /B 0

:EFI
ECHO COPYING EFI
CALL %EXEC_DRIVE_LETTER%\SCRIPTS\EFI.CMD %EXEC_DRIVE_LETTER%
EXIT /B 0

:FIXBOOT
ECHO FIXING BOOT ENTRIES
BOOTREC /FIXBOOT
BOOTREC /REBUILDBCD
EXIT /B 0

:GRUB
ECHO COPYING GRUB
CALL %EXEC_DRIVE_LETTER%\SCRIPTS\GRUB.CMD %EXEC_DRIVE_LETTER%
EXIT /B 0

:UNLOCK
ECHO [UNLOCK]
ECHO:
ECHO 0 ^| UP
ECHO ==========================
ECHO 1 ^| SET TESTSIGNING MODE
ECHO 2 ^| APPLY SECUREBOOT SIGNING WORKAROUND

set /p ID=ENTER ACTION:
ECHO:
IF "%ID%"=="0" GOTO MENU
IF "%ID%"=="1" CALL :TESTSIGNING
IF "%ID%"=="2" CALL :SECUREBOOT
GOTO UNLOCK

:TESTSIGNING
ECHO SETTING TESTSIGNING
CALL %EXEC_DRIVE_LETTER%\SCRIPTS\TESTSIGNING.CMD %EXEC_DRIVE_LETTER%
EXIT /B 0

:SECUREBOOT
ECHO EXECUTING SECUREBOOT SIGNING WORKAROUND
CALL %EXEC_DRIVE_LETTER%\SCRIPTS\SECUREBOOT.CMD %EXEC_DRIVE_LETTER%
EXIT /B 0
